#!/usr/bin/env zsh

# Set up the prompt
autoload -Uz promptinit
promptinit

setopt histignorealldups sharehistory

# Use emacs keybindings even if our EDITOR is set to vi
bindkey -e

# Keep 1000 lines of history within the shell and save it to ~/.zsh_history:
HISTSIZE=10000
SAVEHIST=10000
HISTFILE=~/.zsh_history

# Use modern completion system
autoload -Uz compinit
compinit

zstyle ':completion:*' menu select
zstyle ':completion:*' auto-description 'specify: %d'
zstyle ':completion:*' completer _expand _complete _correct _approximate
zstyle ':completion:*' group-name ''
zstyle ':completion:*' list-colors ''
zstyle ':completion:*' list-prompt %SAt %p: Hit TAB for more, or the character to insert%s
zstyle ':completion:*' matcher-list '' 'm:{a-z}={A-Z}' 'm:{a-zA-Z}={A-Za-z}' 'r:|[._-]=* r:|=* l:|=*'
zstyle ':completion:*' select-prompt %SScrolling active: current selection at %p%s
zstyle ':completion:*' use-compctl false
zstyle ':completion:*' verbose true

zstyle ':completion:*:*:kill:*:processes' list-colors '=(#b) #([0-9]#)*=0=01;31'
zstyle ':completion:*:kill:*' command 'ps -u $USER -o pid,%cpu,tty,cputime,cmd'

# OS specific stuff
OS="$(uname)"

case "$OS" in
	'Darwin')
		# macOS stuff here
		eval "$(/opt/homebrew/bin/brew shellenv)"
		export PNPM_HOME="$HOME/Library/pnpm"

		alias mydns="scutil --dns | grep 'nameserver\[[0-9]*\]'"
		alias update="brew update; brew upgrade; brew cleanup; pnpm self-update; pnpm update --latest -g; echo -e '\nDone!'"
		;;
	'Linux')
		eval "$(dircolors -b)"
		export PNPM_HOME="$HOME/.local/share/pnpm"

		DISTRO="$(cat /etc/os-release 2>/dev/null | grep -e '^NAME=')"

		if [[ "${DISTRO}" =~ "Arch Linux" ]]; then
			alias update="sudo pacman -Syu --noconfirm; yay -Syua --noconfirm; pnpm self-update; pnpm update --latest -g;  echo -e '\nDone!'"
			alias ins='sudo pacman -S'
			alias rem='sudo pacman -Rns'
			alias yins='yay -S'
			alias yrem='yay -Rns'
		elif [[ "${DISTRO}" =~ "Ubuntu" ]]; then
			alias update="sudo apt update -y; sudo apt upgrade -y; sudo apt autoremove -y; pnpm self-update; pnpm update --latest -g; echo -e '\nDone!'"
			alias ins='sudo apt install'
			alias rem='sudo apt autoremove --purge'
		fi
		;;
esac

# Check if antigen is installed; if not, download the script
if [ ! -f "$HOME/.zsh/antigen.zsh" ]; then
	echo "antigen not found, installing it..."
	mkdir -p "$HOME/.zsh"
	curl -L git.io/antigen > "$HOME/.zsh/antigen.zsh"
	clear
fi

# Check if fnm is installed; if not, install it
if [ ! -d "$HOME/.local/share/fnm" ]; then
	echo "fnm not found, installing it..."
	curl -fsSL https://fnm.vercel.app/install | bash -s -- --skip-shell
	clear
fi

# Check if tpm is installed; if not, install it
if [ ! -d "$HOME/.tmux/plugins" ]; then
	echo "tpm not found, installing it..."
	git clone https://github.com/tmux-plugins/tpm ~/.tmux/plugins/tpm
	clear
fi

# Plugins
source "$HOME/.zsh/antigen.zsh"

antigen bundle zsh-users/zsh-autosuggestions
antigen bundle zsh-users/zsh-syntax-highlighting
antigen bundle globalias
antigen bundle shrink-path
antigen bundle agkozak/zsh-z

antigen apply

# Aliases
# Check if eza is installed
if command -v eza &>/dev/null; then
	alias ls='eza --icons'
	alias la='eza --icons -a'
	alias ll='eza --icons -l'
	alias lla='eza --icons -la'
	alias _ls='/usr/bin/ls --color=auto'
else
	alias ls='ls --color=auto'
	alias ll='ls -AlF'
	alias la='ls -A'
	alias lla='ls -lFA'
fi

alias grep='grep --color=auto'
alias fgrep='fgrep --color=auto'
alias egrep='egrep --color=auto'
alias v='vim'
alias pr='pnpm run'
alias ga='git add'
alias gd='git diff'
alias gs='git status'
alias gc='git commit -m'
alias gcn='git commit --no-edit'
alias gcz='git cz'
alias gl="git log --oneline"
alias gf='git fetch'
alias gclo='git clone'
alias gcln='git clean -df'
alias gp='git push'
alias gri='git rebase -i'
alias ncu='pnpm dlx npm-check-updates -u'
alias ytmp3="yt-dlp -f 'ba' -x --audio-format mp3"
alias rndstr='openssl rand -hex 32'
alias c='code'
alias oc='opencode'
alias lg='lazygit'

# Don't expand these aliases
GLOBALIAS_FILTER_VALUES=(ls la ll lla z)

# Git add everything, commit with message and push
gacp() {
	git add .
	git commit -m "$1"
	git push
}

# Colored man pages
export LESS_TERMCAP_mb=$'\E[01;31m'
export LESS_TERMCAP_md=$'\E[01;31m'
export LESS_TERMCAP_me=$'\E[0m'
export LESS_TERMCAP_se=$'\E[0m'
export LESS_TERMCAP_so=$'\E[01;47;34m'
export LESS_TERMCAP_ue=$'\E[0m'
export LESS_TERMCAP_us=$'\E[01;32m'
export LESS=-r

# Editor
export EDITOR="vim"

# Fix shell bindings
bindkey  "^[[1;5C"  forward-word
bindkey  "^[[1;5D"  backward-word
bindkey  "^[[H"     beginning-of-line
bindkey  "^[[F"     end-of-line
bindkey  "^[[3~"    delete-char

# java
export JAVA_HOME=$(/usr/libexec/java_home -v 17)

# bun
export BUN_INSTALL="$HOME/.bun"

# pyenv
export PYENV_ROOT="$HOME/.pyenv"

# bun completions
[ -s "$HOME/.bun/_bun" ] && source "$HOME/.bun/_bun"

# Configure PATH
path=(
	"$JAVA_HOME/bin"
	"$HOME/.local/bin"
	"$HOME/.local/share/bin"
	"$HOME/.opencode/bin"
	"$PYENV_ROOT/bin"(N)
	"$BUN_INSTALL/bin"(N)
	"$PNPM_HOME"(N)
	"$HOME/.local/share/fnm"(N)
	"/snap/bin"(N)
	"$path[@]"
)
typeset -U path
export PATH

# Git
autoload -Uz vcs_info
zstyle ':vcs_info:*' enable git
zstyle ':vcs_info:git*' formats "%b "

# Setup prompt
setopt prompt_subst

precmd() {
	local ec=$?
	vcs_info

	PROMPT="%F{cyan}$(shrink_path -f) "

	if [[ -n ${vcs_info_msg_0_} ]]; then
    local gs=$(git status --porcelain 2>/dev/null)
    local ahead=$(git rev-list --count HEAD@{upstream}..HEAD 2>/dev/null)
    local behind=$(git rev-list --count HEAD..HEAD@{upstream} 2>/dev/null)
    local color=''

    if [[ -n ${gs} ]]; then
      # Working tree has changes
      color="%F{red}"
    elif [[ ${ahead:-0} -gt 0 || ${behind:-0} -gt 0 ]]; then
      # Branch is ahead or behind upstream
      color="%F{yellow}"
    else
      # Clean working tree
      color="%F{green}"
    fi

    PROMPT+="${color}${vcs_info_msg_0_}"
  fi

	if [[ $ec -ne 0 ]]; then
		PROMPT+="%F{red}%B${ec}%b "
	fi

	PROMPT+="%F{magenta}❯%f "
}

# If private aliases file found, source it
if [ -f "$HOME/.zsh_private_aliases" ]; then
	source "$HOME/.zsh_private_aliases"
fi

if command -v pyenv >/dev/null; then
	eval "$(pyenv init -)"
fi

# fnm init
eval "$(fnm env --use-on-cd)"

